// cheap-ruler-java is licensed under the BSD 3-Clause
// License, https://opensource.org/licenses/BSD-3-Clause
//
// Copyright (c) 2020, Ian Emmons. All rights reserved.

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id 'eclipse'
}

group = 'mobi.emmons.cheap_ruler'
version = '1.0.1'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

javadoc {
	title = 'CheapRuler (Java implementation)'
	options.addBooleanOption('Xdoclint:all', true)
	options.addStringOption('Xmaxwarns', '1')
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

test {
	useJUnitPlatform()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		cheapRulerJavaPub(MavenPublication) {
			from components.java
			pom {
				name = 'cheap-ruler-java'
				description = 'Fast approximations for common geodesic measurements'
				url = 'https://github.com/IanEmmons/cheap-ruler-java'
				licenses {
					license {
						name = 'BSD 3-Clause License'
						url = 'https://github.com/IanEmmons/cheap-ruler-java/blob/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'IanEmmons'
						name = 'Ian Emmons'
						email = 'ian@emmons.mobi'
					}
				}
				scm {
					connection = 'git@github.com:IanEmmons/cheap-ruler-java.git'
					developerConnection = 'git@github.com:IanEmmons/cheap-ruler-java.git'
					url = 'https://github.com/IanEmmons/cheap-ruler-java'
				}
			}
		}
	}

	repositories {
		//mavenCentral()
		//jcenter()
		//maven {
		//	name = 'MavenFileRepo'
		//	url = "file://${System.env.MAVEN_FILE_REPO}"
		//}
		maven {
			name = 'SonatypeOssrhRepo'
			url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
			credentials {
				username "$sonatypeOssrhUser"
				password "$sonatypeOssrhPassword"
			}
		}
	}
}

signing {
	sign publishing.publications.cheapRulerJavaPub
}
